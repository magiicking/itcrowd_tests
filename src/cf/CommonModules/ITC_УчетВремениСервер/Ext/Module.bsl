
//Возвращает задачу в иерархии перебирая снизу вверх
//Перебор идёт по реквизиту ДокументОснование
Функция ПолучитьЗадачу(пСсылка) Экспорт
	Если ТипЗнч(пСсылка) = Тип("ДокументСсылка.Задача")
		ИЛИ ТипЗнч(пСсылка) = Тип("СправочникСсылка.ITC_Задача")
		ИЛИ ТипЗнч(пСсылка) = Тип("СправочникСсылка.итк_Задача") Тогда
		Возврат пСсылка;
	ИначеЕсли ЗначениеЗаполнено(пСсылка.ДокументОснование) Тогда
		Возврат ПолучитьЗадачу(пСсылка.ДокументОснование);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Процедура УстановитьСтатусДокумента(пДокумент, пСтатус) Экспорт
	яДата = ТекущаяДата();
	яТекущийСтатус = ПолучитьСтатусДокумента(пДокумент, яДата);
	Если яТекущийСтатус = Неопределено ИЛИ яТекущийСтатус.Статус <> пСтатус Тогда
		яЗапись = РегистрыСведений.СтатусыДокументов.СоздатьМенеджерЗаписи();
		яЗапись.Документ	= пДокумент;
		яЗапись.Период		= яДата;
		яЗапись.Статус		= пСтатус;
		яЗапись.Записать();
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьСтатусДокумента(пДокумент, пДата = Неопределено) Экспорт
	яОтбор = Новый Структура("Документ", пДокумент);
	яЗапись = РегистрыСведений.СтатусыДокументов.ПолучитьПоследнее(?(пДата=Неопределено, ТекущаяДата(), пДата), яОтбор);
	Возврат яЗапись;
КонецФункции