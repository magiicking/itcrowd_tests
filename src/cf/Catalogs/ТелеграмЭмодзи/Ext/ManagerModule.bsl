Процедура ЗагрузитьСлепок(ИмяСлепка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмЭмодзи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТелеграмЭмодзи КАК ТелеграмЭмодзи
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмЭмодзи.ЭтоГруппа";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Спр = Выборка.Ссылка.ПолучитьОбъект();
		Спр.Удалить();
	КонецЦикла;
	
	СтрокаJSON = Справочники.ТелеграмЭмодзи.ПолучитьМакет(ИмяСлепка).ПолучитьТекст();
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Массив = ПрочитатьJSON(ЧтениеJSON);
	Для Каждого Структура Из Массив Цикл
		Если Структура.ЭтоГруппа Тогда
			Спр = Справочники.ТелеграмЭмодзи.СоздатьГруппу();
			ЗаполнитьЗначенияСвойств(Спр, Структура);
			Спр.Записать();
		Иначе
			Спр = Справочники.ТелеграмЭмодзи.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(Спр, Структура, , "Картинка");
			Спр.Картинка = Новый ХранилищеЗначения(Base64Значение(Структура.Картинка));
			Спр.Родитель = Справочники.ТелеграмЭмодзи.НайтиПоНаименованию(Структура.РодительНаименование, Истина, Справочники.ТелеграмЭмодзи.ПустаяСсылка());
			Спр.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


Функция ВыгрузитьСлепок() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмЭмодзи.Родитель.Наименование КАК РодительНаименование,
	|	ТелеграмЭмодзи.ЭтоГруппа КАК ЭтоГруппа,
	|	ТелеграмЭмодзи.Код КАК Код,
	|	ТелеграмЭмодзи.Наименование КАК Наименование,
	|	ТелеграмЭмодзи.Картинка КАК Картинка,
	|	ТелеграмЭмодзи.Популярность КАК Популярность,
	|	ТелеграмЭмодзи.НомерКартинки КАК НомерКартинки
	|ИЗ
	|	Справочник.ТелеграмЭмодзи КАК ТелеграмЭмодзи
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГруппа УБЫВ,
	|	Наименование
	|ИТОГИ ПО
	|	ЭтоГруппа";
	
	Массив = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	ВыборкаГруппы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ЭтоГруппа");
	Пока ВыборкаГруппы.Следующий() Цикл
		Выборка = ВыборкаГруппы.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ЭтоГруппа Тогда
				Структура = Новый Структура("ЭтоГруппа,Код,Наименование");
				ЗаполнитьЗначенияСвойств(Структура, Выборка);
			Иначе
				Структура = Новый Структура("РодительНаименование,ЭтоГруппа,Код,Наименование,
				|Картинка,Популярность,НомерКартинки");
				ЗаполнитьЗначенияСвойств(Структура, Выборка, , "Картинка");
				Структура.Картинка = Base64Строка(Выборка.Картинка.Получить());
			КонецЕсли;
			Массив.Добавить(Структура);
		КонецЦикла;
	КонецЦикла;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(ТелеграмСервер.ПараметрыЗаписиJSON());
	ЗаписатьJSON(ЗаписьJSON, Массив);
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции