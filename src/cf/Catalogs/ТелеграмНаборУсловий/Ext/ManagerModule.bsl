Функция ОтфильтрованныеСервисы(Сервисы, НакопленныеДанные) Экспорт
	
	Ответ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Сервисы", Сервисы);
	Запрос.УстановитьПараметр("Бот", НакопленныеДанные.ДанныеБота.Бот);
	Запрос.УстановитьПараметр("ИдентификаторЧата", НакопленныеДанные.ИдентификаторЧата);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмСервисНаборыУсловий.Ссылка КАК Сервис,
	|	ТелеграмСервисНаборыУсловий.НаборУсловий КАК НаборУсловий
	|ПОМЕСТИТЬ НаборыУсловий
	|ИЗ
	|	Справочник.ТелеграмСервис.НаборыУсловий КАК ТелеграмСервисНаборыУсловий
	|ГДЕ
	|	ТелеграмСервисНаборыУсловий.Ссылка В(&Сервисы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмКонтекстСеансов.Параметр КАК Параметр,
	|	ТелеграмКонтекстСеансов.Значение КАК ЛевоеЗначение
	|ПОМЕСТИТЬ КонтекстСеанса
	|ИЗ
	|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
	|ГДЕ
	|	ТелеграмКонтекстСеансов.Бот = &Бот
	|	И ТелеграмКонтекстСеансов.ИдентификаторЧата = &ИдентификаторЧата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборыУсловий.Сервис КАК Сервис,
	|	НаборыУсловий.НаборУсловий КАК НаборУсловий
	|ИЗ
	|	НаборыУсловий КАК НаборыУсловий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтекстСеанса.Параметр КАК Параметр,
	|	КонтекстСеанса.ЛевоеЗначение КАК Значение
	|ИЗ
	|	КонтекстСеанса КАК КонтекстСеанса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмНаборУсловий.Ссылка КАК НаборУсловий,
	|	ТелеграмНаборУсловий.ПрограммнаяПроверка КАК ПрограммнаяПроверка,
	|	ТелеграмНаборУсловий.КодПроверки КАК КодПроверки
	|ИЗ
	|	Справочник.ТелеграмНаборУсловий КАК ТелеграмНаборУсловий
	|ГДЕ
	|	ТелеграмНаборУсловий.Ссылка В
	|			(ВЫБРАТЬ
	|				НаборыУсловий.НаборУсловий
	|			ИЗ
	|				НаборыУсловий)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмНаборУсловийКонтекст.Ссылка КАК НаборУсловий,
	|	ТелеграмНаборУсловийКонтекст.Параметр КАК Параметр,
	|	КонтекстСеанса.ЛевоеЗначение КАК ЛевоеЗначение,
	|	ТелеграмНаборУсловийКонтекст.ВидСравнения КАК ВидСравнения,
	|	ТелеграмНаборУсловийКонтекст.Значение КАК ПравоеЗначение
	|ИЗ
	|	Справочник.ТелеграмНаборУсловий.Контекст КАК ТелеграмНаборУсловийКонтекст
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтекстСеанса КАК КонтекстСеанса
	|		ПО ТелеграмНаборУсловийКонтекст.Параметр = КонтекстСеанса.Параметр
	|ГДЕ
	|	ТелеграмНаборУсловийКонтекст.Ссылка В
	|			(ВЫБРАТЬ
	|				НаборыУсловий.НаборУсловий
	|			ИЗ
	|				НаборыУсловий)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмНаборУсловийКонтекст.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмНаборУсловийУсловия.Ссылка КАК НаборУсловий,
	|	ТелеграмНаборУсловийУсловия.Параметр КАК Параметр,
	|	ТелеграмНаборУсловийУсловия.ВидСравнения КАК ВидСравнения,
	|	ТелеграмНаборУсловийУсловия.Значение КАК ПравоеЗначение
	|ИЗ
	|	Справочник.ТелеграмНаборУсловий.Условия КАК ТелеграмНаборУсловийУсловия
	|ГДЕ
	|	ТелеграмНаборУсловийУсловия.Ссылка В
	|			(ВЫБРАТЬ
	|				НаборыУсловий.НаборУсловий
	|			ИЗ
	|				НаборыУсловий)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмНаборУсловийУсловия.НомерСтроки";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ТаблицаНаборовУсловий 				= Пакет[2].Выгрузить();
	ТаблицаКонтекста					= Пакет[3].Выгрузить();
	Контекст 							= ТелеграмСервер.ПреобразоватьТаблицуКонтекстаВСоответствие(ТаблицаКонтекста);
	
	НаборыУсловийПрограммнаяПроверка 	= Пакет[4].Выгрузить();
	НаборыУсловийКонтекст 				= Пакет[5].Выгрузить();
	НаборыУсловийУсловия 				= Пакет[6].Выгрузить();
	
	СервисыИсключить = Новый Массив;
	
	ПоляПоиска = Новый Структура("НаборУсловий");
	Для Каждого СтрокаТаблицыНабораУсловий Из ТаблицаНаборовУсловий Цикл
		
		Сервис = СтрокаТаблицыНабораУсловий.Сервис;
		ПоляПоиска.НаборУсловий = СтрокаТаблицыНабораУсловий.НаборУсловий;
		
		УсловияИстинны = Истина;
		
		// Программная проверка
		Найдены = НаборыУсловийПрограммнаяПроверка.НайтиСтроки(ПоляПоиска);
		Для Каждого Найден Из Найдены Цикл
			Если Найден.ПрограммнаяПроверка = Истина Тогда
				НакопленныеДанные.Вставить("КонтекстСеанса", Контекст);
				УсловияИстинны = ПрограммнаяПроверка_УсловиеИстинно(Найден.КодПроверки, НакопленныеДанные);
				Если УсловияИстинны = Ложь Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если УсловияИстинны = Ложь Тогда
			СервисыИсключить.Добавить(Сервис);
			Продолжить;
		КонецЕсли;
		
		// Проверка контекста
		Найдены = НаборыУсловийКонтекст.НайтиСтроки(ПоляПоиска);
		Для Каждого Найден Из Найдены Цикл
			УсловияИстинны = ТелеграмСервер.СравнениеИстинно(Найден.ЛевоеЗначение, Найден.ВидСравнения, Найден.ПравоеЗначение);
			Если УсловияИстинны = Ложь Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если УсловияИстинны = Ложь Тогда
			СервисыИсключить.Добавить(Сервис);
			Продолжить;
		КонецЕсли;
		
		// Проверка условий содержания сообщения
		Найдены = НаборыУсловийУсловия.НайтиСтроки(ПоляПоиска);
		Для Каждого Найден Из Найдены Цикл
			ЛевоеЗначение = ПланыВидовХарактеристик.ТелеграмПараметрыВходящегоСообщения.ЗначениеПараметраВходящегоСообщения(Найден.Параметр, НакопленныеДанные.Сообщение);
			УсловияИстинны = ТелеграмСервер.СравнениеИстинно(ЛевоеЗначение, Найден.ВидСравнения, Найден.ПравоеЗначение);
			Если УсловияИстинны = Ложь Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если УсловияИстинны = Ложь Тогда
			СервисыИсключить.Добавить(Сервис);
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Сервис Из Сервисы Цикл
		Если СервисыИсключить.Найти(Сервис) = Неопределено Тогда
			Ответ.Добавить(Сервис);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции


Функция ПрограммнаяПроверка_УсловиеИстинно(КодПроверки, НакопленныеДанные) Экспорт
	
	Ответ = Ложь;
	Выполнить(КодПроверки);
	Возврат Ответ;
	
КонецФункции


Функция ПроверкаНабораУсловий(НаборУсловий, НакопленныеДанные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НаборУсловий", НаборУсловий);
	Запрос.УстановитьПараметр("ИдентификаторЧата", НакопленныеДанные.ИдентификаторЧата);
	Запрос.УстановитьПараметр("Бот", НакопленныеДанные.ДанныеБота.Бот);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмКонтекстСеансов.Параметр КАК Параметр,
	|	ТелеграмКонтекстСеансов.Значение КАК ЛевоеЗначение
	|ПОМЕСТИТЬ КонтекстСеанса
	|ИЗ
	|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
	|ГДЕ
	|	ТелеграмКонтекстСеансов.Бот = &Бот
	|	И ТелеграмКонтекстСеансов.ИдентификаторЧата = &ИдентификаторЧата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтекстСеанса.Параметр КАК Параметр,
	|	КонтекстСеанса.ЛевоеЗначение КАК Значение
	|ИЗ
	|	КонтекстСеанса КАК КонтекстСеанса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмНаборУсловий.ПрограммнаяПроверка КАК ПрограммнаяПроверка,
	|	ТелеграмНаборУсловий.КодПроверки КАК КодПроверки
	|ИЗ
	|	Справочник.ТелеграмНаборУсловий КАК ТелеграмНаборУсловий
	|ГДЕ
	|	ТелеграмНаборУсловий.Ссылка = &НаборУсловий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмНаборУсловийКонтекст.Параметр КАК Параметр,
	|	КонтекстСеанса.ЛевоеЗначение КАК ЛевоеЗначение,
	|	ТелеграмНаборУсловийКонтекст.ВидСравнения КАК ВидСравнения,
	|	ТелеграмНаборУсловийКонтекст.Значение КАК ПравоеЗначение
	|ИЗ
	|	Справочник.ТелеграмНаборУсловий.Контекст КАК ТелеграмНаборУсловийКонтекст
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтекстСеанса КАК КонтекстСеанса
	|		ПО ТелеграмНаборУсловийКонтекст.Параметр = КонтекстСеанса.Параметр
	|ГДЕ
	|	ТелеграмНаборУсловийКонтекст.Ссылка = &НаборУсловий
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмНаборУсловийКонтекст.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмНаборУсловийУсловия.Параметр КАК Параметр,
	|	ТелеграмНаборУсловийУсловия.ВидСравнения КАК ВидСравнения,
	|	ТелеграмНаборУсловийУсловия.Значение КАК ПравоеЗначение
	|ИЗ
	|	Справочник.ТелеграмНаборУсловий.Условия КАК ТелеграмНаборУсловийУсловия
	|ГДЕ
	|	ТелеграмНаборУсловийУсловия.Ссылка = &НаборУсловий
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмНаборУсловийУсловия.НомерСтроки";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	ТаблицаКонтекстСеансов		= Пакет[1].Выгрузить();	
	Контекст 					= ТелеграмСервер.ПреобразоватьТаблицуКонтекстаВСоответствие(ТаблицаКонтекстСеансов);
	ТаблицаПрограммнаяПроверка 	= Пакет[2].Выгрузить();
	ТаблицаКонтекст				= Пакет[3].Выгрузить();
	ТаблицаУсловия				= Пакет[4].Выгрузить();
	
	УсловияИстинны = Истина;
	
	Для Каждого Найден Из ТаблицаПрограммнаяПроверка Цикл
		Если Найден.ПрограммнаяПроверка = Истина Тогда
			НакопленныеДанные.Вставить("Контекст", Контекст);
			УсловияИстинны = ПрограммнаяПроверка_УсловиеИстинно(Найден.КодПроверки, НакопленныеДанные);
			Если УсловияИстинны = Ложь Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если УсловияИстинны = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Найден Из ТаблицаКонтекст Цикл
		УсловияИстинны = ТелеграмСервер.СравнениеИстинно(Найден.ЛевоеЗначение, Найден.ВидСравнения, Найден.ПравоеЗначение);
		Если УсловияИстинны = Ложь Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если УсловияИстинны = Ложь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Найден Из ТаблицаУсловия Цикл
		ЛевоеЗначение = ПланыВидовХарактеристик.ТелеграмПараметрыВходящегоСообщения.ЗначениеПараметраВходящегоСообщения(Найден.Параметр, НакопленныеДанные.Сообщение);
		УсловияИстинны = ТелеграмСервер.СравнениеИстинно(ЛевоеЗначение, Найден.ВидСравнения, Найден.ПравоеЗначение);
		Если УсловияИстинны = Ложь Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат УсловияИстинны;
	
КонецФункции