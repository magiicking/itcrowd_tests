
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	яЛогоПасс = Новый Структура("Логин,Пароль");
	ЗаполнитьЗначенияСвойств(яЛогоПасс, ЭтаФорма);
	яХранЛП = Новый ХранилищеЗначения(яЛогоПасс);
	ТекущийОбъект.ЛогинПароль = яХранЛП;
	Если ЗначениеЗаполнено(АдресХранилища) И ЭтаФорма.Модифицированность Тогда
		ТекущийОбъект.Данные = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресХранилища),Новый СжатиеДанных(9));	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	яЛогоПасс = РеквизитФормыВЗначение("Объект").ЛогинПароль.Получить();
	Если ТипЗнч(яЛогоПасс) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, яЛогоПасс);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьДанныеИзФайла(Команда)
	ДВФ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДВФ.Фильтр = "*.dt|*.dt";
	РезультатВыбора = Ждать ДВФ.ВыбратьАсинх();
	Если РезультатВыбора <> Неопределено Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(РезультатВыбора[0]), ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СохранитьДанныеВФайлНаСервере()
	ВремОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ПоместитьВоВременноеХранилище(ВремОбъект.Данные.Получить(),ЭтаФорма.УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Асинх Процедура СохранитьДанныеВФайл(Команда)
	ДВФ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДВФ.Фильтр = "*.dt|*.dt";
	РезультатВыбора = Ждать ДВФ.ВыбратьАсинх();
	Если РезультатВыбора <> Неопределено Тогда
		АдресХранилища = СохранитьДанныеВФайлНаСервере();
		ДанныеФайла = ПолучитьИзВременногоХранилища(АдресХранилища);
		ДанныеФайла.Записать(РезультатВыбора[0]);
	КонецЕсли;
КонецПроцедуры
