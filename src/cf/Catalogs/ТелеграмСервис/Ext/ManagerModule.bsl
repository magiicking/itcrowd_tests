Функция ПолучитьСервисыДляВыполнения(НакопленныеДанные) Экспорт
	
	Перем ВидСравнения;
	
	Ответ = Новый Массив;
	
	Сообщение 					= НакопленныеДанные.Сообщение;
	ИдентификаторЧата 			= НакопленныеДанные.ИдентификаторЧата;
	ВидВходящегоОбновления 		= НакопленныеДанные.ВидВходящегоОбновления;
	
	Если ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.Сообщение Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаСравнения.Ссылка КАК Сервис,
		|	ТаблицаСравнения.Ссылка.Сортировка КАК Сортировка,
		|	ТаблицаСравнения.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСравнения.ВидСравнения КАК ВидСравнения,
		|	ТаблицаСравнения.Значение КАК Значение
		|ИЗ
		|	Справочник.ТелеграмСервис.ВходящийТекст КАК ТаблицаСравнения
		|ГДЕ
		|	ТаблицаСравнения.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.ТелеграмСтатусыИспользования.Используется)
		|	И ТаблицаСравнения.Ссылка.Бот = &Бот
		|	И ТаблицаСравнения.Ссылка.ВидВходящегоОбновления = &ВидВходящегоОбновления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сортировка";
		
		ЛевоеЗначение = НакопленныеДанные.ТекстСообщения;
		
	ИначеЕсли ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.ОтветКонтекстнойКлавиатуры Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСравнения.Ссылка КАК Сервис,
		|	ТаблицаСравнения.Ссылка.Сортировка КАК Сортировка,
		|	ТаблицаСравнения.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСравнения.ВидСравнения КАК ВидСравнения,
		|	ТаблицаСравнения.Значение КАК Значение
		|ИЗ
		|	Справочник.ТелеграмСервис.ВходящиеДанные КАК ТаблицаСравнения
		|ГДЕ
		|	ТаблицаСравнения.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.ТелеграмСтатусыИспользования.Используется)
		|	И ТаблицаСравнения.Ссылка.Бот = &Бот
		|	И ТаблицаСравнения.Ссылка.ВидВходящегоОбновления = &ВидВходящегоОбновления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сортировка";
		
		ЛевоеЗначение = НакопленныеДанные.ОтветКонтекстнойКлавиатуры;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", НакопленныеДанные.ДанныеБота.Бот);
	Запрос.УстановитьПараметр("ВидВходящегоОбновления", ВидВходящегоОбновления);
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Сервис 			= Выборка.Сервис;
		ВидСравнения 	= Выборка.ВидСравнения;
		ПравоеЗначение 	= Выборка.Значение;
		
		Если ТелеграмСервер.СравнениеИстинно(ЛевоеЗначение, ВидСравнения, ПравоеЗначение) Тогда
			Если Ответ.Найти(Сервис) = Неопределено Тогда
				Ответ.Добавить(Сервис);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Ответ = Справочники.ТелеграмНаборУсловий.ОтфильтрованныеСервисы(Ответ, НакопленныеДанные);
	
	Возврат Ответ;
	
КонецФункции