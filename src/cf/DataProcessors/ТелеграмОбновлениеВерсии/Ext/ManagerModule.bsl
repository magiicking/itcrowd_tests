Процедура ПерейтиНаВерсию1_00() Экспорт
	
	РегистрыСведений.ТелеграмМетоды.ЗагрузитьСлепок("Слепок20170525");
	Сообщить("Загружены данные в регистр ""Описание параметров методов""");
	
	Справочники.ТелеграмЭмодзи.ЗагрузитьСлепок("Слепок20170525");
	Сообщить("Загружена коллекция Эмодзи");
	
	УстановитьКонстантуВерсии("1.00");
	
КонецПроцедуры


Процедура ПерейтиНаВерсию1_01() Экспорт
	
	// ничего добавлять/изменять не требуется
	УстановитьКонстантуВерсии("1.01");
	
КонецПроцедуры


Процедура ПерейтиНаВерсию1_02() Экспорт
	
	ЗаполнитьИзмерениеБотРегистраТелеграмКонтекстСеансов();
	ДобавитьМетоды1_02();
	
	УстановитьКонстантуВерсии("1.02");
	
КонецПроцедуры


Процедура УстановитьКонстантуВерсии(НомерВерсии)
	
	МенеджерЗначения = Константы.ТелеграмТекущаяВерсия.СоздатьМенеджерЗначения();
	МенеджерЗначения.ДополнительныеСвойства.Вставить("Обновление");
	МенеджерЗначения.Значение = НомерВерсии;
	МенеджерЗначения.Записать();
	Сообщить("Переход на версию " + НомерВерсии + " завершен");
	
КонецПроцедуры


Процедура ЗаполнитьИзмерениеБотРегистраТелеграмКонтекстСеансов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ВложенныйЗапрос.Ботов, 0) КАК Ботов
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(1) КАК Ботов
	|	ИЗ
	|		Справочник.ТелеграмБоты КАК ТелеграмБоты) КАК ВложенныйЗапрос";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	КоличествоБотов = Выборка.Ботов;
	
	Если КоличествоБотов = 0 Тогда
		Возврат;
	ИначеЕсли КоличествоБотов > 1 Тогда
		ТекстСообщения = "Не получилось заполнить измерение Бот регистра сведений ТелеграмКонтекстСеансов, так как ботов больше одного. Вам придётся сделать это самостоятельно.";
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмБоты.Ссылка
	|ИЗ
	|	Справочник.ТелеграмБоты КАК ТелеграмБоты";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Бот = Выборка.Ссылка;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмКонтекстСеансов.Бот,
	|	ТелеграмКонтекстСеансов.ИдентификаторЧата,
	|	ТелеграмКонтекстСеансов.Параметр,
	|	ТелеграмКонтекстСеансов.Значение
	|ИЗ
	|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
	|ГДЕ
	|	ТелеграмКонтекстСеансов.Бот = ЗНАЧЕНИЕ(Справочник.ТелеграмБоты.ПустаяСсылка)";
	
	НачатьТранзакцию();
	
	НаборЗаписей = РегистрыСведений.ТелеграмКонтекстСеансов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Бот.Установить(Справочники.ТелеграмБоты.ПустаяСсылка());
	НаборЗаписей.Записать(Истина);
	
	НаборЗаписей = РегистрыСведений.ТелеграмКонтекстСеансов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Бот.Установить(Бот);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Бот = Бот;
		НоваяЗапись.ИдентификаторЧата = Выборка.ИдентификаторЧата;
		НоваяЗапись.Параметр = Выборка.Параметр;
		НоваяЗапись.Значение = Выборка.Значение;
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры


Процедура ДобавитьМетоды1_02()
	
	РегистрыСведений.ТелеграмМетоды.ЗагрузитьСлепок("Обновление20171121", Ложь);
	Сообщить("Загружены новые данные в регистр ""Описание параметров методов"":");
	Сообщить("Добавлены параметры метода editMessageText");
	Сообщить("Добавлены параметры метода deleteMessage");
	
КонецПроцедуры


Функция ПолучитьОписаниеОбновления(НомерВерсии) Экспорт
	
	ИмяМакета = "ОписаниеОбновления" + НомерВерсии;
	ТекстОбновления = Обработки.ТелеграмОбновлениеВерсии.ПолучитьМакет(ИмяМакета).ПолучитьТекст();
	Возврат ТекстОбновления;
	
КонецФункции