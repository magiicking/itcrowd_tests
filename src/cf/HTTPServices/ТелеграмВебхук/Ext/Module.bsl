Функция ЛюбойPost(Запрос)
	
	ВебхукКлюч = Запрос.ПараметрыURL["Ключ"];
	ДанныеБота = ТелеграмСервер.ПолучитьДанныеБотаПоКлючу(ВебхукКлюч);
	Если ДанныеБота = Неопределено Тогда
		Ответ = Новый HTTPСервисОтвет(404);
		Возврат Ответ;
	КонецЕсли;
	
	СтрокаJSON = Запрос.ПолучитьТелоКакСтроку();
	
	РегистрыСведений.ТелеграмЖурнал.ДобавитьЗапись(ДанныеБота.Бот, СтрокаJSON, "Webhook", "ВХ");
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	
	Обновление = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON);
	ТелеграмСервер.ОбработатьВходящееОбновлениеБота(Обновление, ДанныеБота);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Возврат Ответ;
	
КонецФункции


Функция ЛюбойGet(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	Тело = "<HTML><HEAD><meta charset=""utf-8""></HEAD><BODY>Привет, я GET-метод бота Telegram. Ключ: " + Запрос.ПараметрыURL["Ключ"] + "</BODY></HTML>";
	Ответ.УстановитьТелоИзСтроки(Тело, КодировкаТекста.UTF8);
	Возврат Ответ;
	
КонецФункции
