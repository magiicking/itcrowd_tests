
&НаКлиенте
Процедура ВыбратьПапку(Команда)
	// Вставить содержимое обработчика.
	// Вставить содержимое обработчика.
	Попытка 
		Аутлук = Новый COMОбъект("Outlook.Application");
	Исключение
		Сообщить("Не доступен программный интерфейс к Аутлуку");
		Возврат;
	КонецПопытки;
	
	ПространствоИмен = Аутлук.GetNamespace("MAPI");
	Папка = ПространствоИмен.PickFolder();
	
	ПапкаИмя = Папка.Name;
	ПапкаИдентификатор = Папка.EntryID;
	
КонецПроцедуры

Процедура ЗаполнитьНарядамиНаСервере()
	
	врТаблицаСобытий =  ТаблицаСобытий.Выгрузить();
	Номера = врТаблицаСобытий.ВыгрузитьКолонку("нНомер");
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсходныеДанные.Ссылка КАК Ссылка,
	|	ИсходныеДанные.Номер КАК Номер,
	|	ИсходныеДанные.Задача КАК Задача,
	|	ИсходныеДанные.Адрес КАК Адрес,
	|	ИсходныеДанные.ПланДатаНачала КАК ПланДатаНачала,
	|	ИсходныеДанные.ПланДатаОкончания КАК ПланДатаОкончания,
	|	ИсходныеДанные.Клиент КАК Клиент,
	|	ИсходныеДанные.Тема КАК Тема,
	|	ИсходныеДанные.ЧтоСделать КАК ЧтоСделать,
	|	ИсходныеДанные.ОписаниеЗадачи КАК ОписаниеЗадачи
	|ИЗ
	|	(ВЫБРАТЬ
	|		Наряд.Ссылка КАК Ссылка,
	|		Наряд.Номер КАК Номер,
	|		Наряд.ДокументОснование КАК Задача,
	|		ВЫРАЗИТЬ(Наряд.Адрес КАК СТРОКА(250)) КАК Адрес,
	|		Наряд.ПланДатаНачала КАК ПланДатаНачала,
	|		Наряд.ПланДатаОкончания КАК ПланДатаОкончания,
	|		Наряд.ДокументОснование.Клиент КАК Клиент,
	|		ВЫРАЗИТЬ(Наряд.ДокументОснование.Тема КАК СТРОКА(250)) КАК Тема,
	|		ВЫРАЗИТЬ(Наряд.Описание КАК СТРОКА(250)) КАК ЧтоСделать,
	|		ВЫРАЗИТЬ(Наряд.ДокументОснование.Описание КАК СТРОКА(250)) КАК ОписаниеЗадачи
	|	ИЗ
	|		Документ.Наряд КАК Наряд
	|	ГДЕ
	|		Наряд.Проведен
	|		И Наряд.Ответственный = &Ответственный
	|		И (Наряд.ПланДатаНачала МЕЖДУ &Начало И &Конец
	|				ИЛИ Наряд.ПланДатаОкончания МЕЖДУ &Начало И &Конец)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Наряд.Ссылка,
	|		Наряд.Номер,
	|		Наряд.ДокументОснование,
	|		ВЫРАЗИТЬ(Наряд.Адрес КАК СТРОКА(250)),
	|		Наряд.ПланДатаНачала,
	|		Наряд.ПланДатаОкончания,
	|		Наряд.ДокументОснование.Клиент,
	|		ВЫРАЗИТЬ(Наряд.ДокументОснование.Тема КАК СТРОКА(250)),
	|		ВЫРАЗИТЬ(Наряд.Описание КАК СТРОКА(250)),
	|		ВЫРАЗИТЬ(Наряд.ДокументОснование.Описание КАК СТРОКА(250))
	|	ИЗ
	|		Документ.Наряд КАК Наряд
	|	ГДЕ
	|		Наряд.Номер В(&Номера)) КАК ИсходныеДанные";
	Запрос.УстановитьПараметр("Начало",Период.ДатаНачала);
	Запрос.УстановитьПараметр("Конец", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Ответственный", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Номера", Номера);
	
	ТаблицаНарядов = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаНаряда ИЗ ТаблицаНарядов Цикл
		
		СтрокаСобытий = врТаблицаСобытий.Найти(СтрокаНаряда.Номер, "нНомер");
		Если СтрокаСобытий = Неопределено Тогда
			СтрокаСобытий = врТаблицаСобытий.Добавить();
			
			СтрокаСобытий.Тема = "["+СокрЛП(СтрокаНаряда.Клиент)+"] "+СокрЛП(СтрокаНаряда.ЧтоСделать);
			
			ШаблонТела = "Тема: %Тема%
			|
			|Описание задачи
			|%ОписаниеЗадачи%
			|=========================
			|{{Номер наряда[%Номер%]}}";
			
			Тело = СтрЗаменить(ШаблонТела, "%Номер%", СтрокаНаряда.Номер);
			Тело = СтрЗаменить(Тело, "%Тема%", СтрокаНаряда.Тема);
			Тело = СтрЗаменить(Тело, "%ОписаниеЗадачи%", СтрокаНаряда.ОписаниеЗадачи);
			
			СтрокаСобытий.Тело = Тело; 
			СтрокаСобытий.ДатаНачала = СтрокаНаряда.ПланДатаНачала;
			СтрокаСобытий.ДатаОкончания = СтрокаНаряда.ПланДатаОкончания;

		КонецЕсли;
		
		СтрокаСобытий.Наряд = СтрокаНаряда.Ссылка;

	КонецЦикла;
	
	ТаблицаСобытий.Загрузить(врТаблицаСобытий);
КонецПроцедуры


&НаКлиенте
Процедура Загрузить(Команда)
	// Вставить содержимое обработчика.
	
	Если не ЗначениеЗаполнено(ПапкаИдентификатор) Тогда
		Сообщить("Выбери папку для синхронизации");
		Возврат;
	КонецЕсли;
	
	Попытка 
		Аутлук = Новый COMОбъект("Outlook.Application");
	Исключение
		Сообщить("Не доступен программный интерфейс к Аутлуку");
		Возврат;
	КонецПопытки;
	
	ПространствоИмен = Аутлук.GetNamespace("MAPI");
	Папка = ПространствоИмен.GetFolderFromID(ПапкаИдентификатор);
	
	Фильтр = "[Start] > '"+Формат(Период.ДатаНачала, "ДФ='dd/MM/yy hh:mm;ss'")+"' AND [Start] < '"+ Формат(Период.ДатаОкончания, "ДФ='dd/MM/yy hh:mm;ss'")+"'";
	
	Таблица = Папка.GetTable(Фильтр);
	ТаблицаСобытий.Очистить();
	Пока НЕ Таблица.EndOfTable Цикл
		Строка = Таблица.GetNextRow();
		
		СтрокаСобытий = ТаблицаСобытий.Добавить();
		СтрокаСобытий.ИД = Строка.Item("EntryID");
		Событие = ПространствоИмен.GetItemFromID(СтрокаСобытий.ИД);
		СтрокаСобытий.Тело = Событие.Body;
		СтрокаСобытий.Тема = Событие.Subject;
		СтрокаСобытий.ДатаНачала = Событие.Start;
		СтрокаСобытий.ДатаОкончания = Событие.End;

		
		стрНачало = СтрНайти(СтрокаСобытий.Тело, "{{Номер наряда[");
		Если стрНачало >0 Тогда 
			стрКонец = СтрНайти(СтрокаСобытий.Тело, "]}}",,стрНачало);
			НомерНаряда = Сред(СтрокаСобытий.Тело, СтрНачало+15,СтрКонец-стрНачало-15);
			СтрокаСобытий.нНомер = НомерНаряда;
		КонецЕсли;		
	КонецЦикла;
	
	ЗаполнитьНарядамиНаСервере();
	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Вставить содержимое обработчика
	Период.Вариант = ВариантСтандартногоПериода.СледующаяНеделя;
КонецПроцедуры

&НаСервереБезКонтекста
Функция НомерПоСсылке(Наряд)
	Возврат Наряд.Номер;	
КонецФункции

&НаКлиенте
Процедура ТаблицаСобытийНарядПриИзменении(Элемент)
	// Вставить содержимое обработчика.
	Если не ЗначениеЗаполнено(ПапкаИдентификатор) Тогда
		Сообщить("Выбери папку для синхронизации");
		Возврат;
	КонецЕсли;

	Попытка 
		Аутлук = Новый COMОбъект("Outlook.Application");
	Исключение
		Сообщить("Не доступен программный интерфейс к Аутлуку");
		Возврат;
	КонецПопытки;
	ПространствоИмен = Аутлук.GetNamespace("MAPI");
	Строка =  Элементы.ТаблицаСобытий.ТекущиеДанные;
	Строка.нНомер = НомерПоСсылке(Строка.Наряд);
	Если ЗначениеЗаполнено(Строка.ИД) Тогда
		Событие = ПространствоИмен.GetItemFromID(Строка.ИД);
		Тело = Событие.Body;
		стрНачало = СтрНайти(Тело, "{{Номер наряда[");
		Если стрНачало = 0 Тогда
			Тело = Тело + Символы.ПС+"{{Номер наряда["+СокрЛП(Строка.нНомер)+"]}}";
			Событие.Body = Тело;
			Событие.Save();
		КонецЕсли;		
	КонецЕсли;	
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьНаряд(Изм)
	Если Изм.Наряд.ПланДатаНачала <> Изм.Начало Или Изм.Наряд.ПланДатаОкончания <> Изм.Конец Тогда
		НарядОбъект = Изм.Наряд.ПолучитьОбъект();
		НарядОбъект.ПланДатаНачала = Изм.Начало;
		НарядОбъект.ПланДатаОкончания = Изм.Конец;
		НарядОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура ОбновитьАутлук(Команда)
	// Вставить содержимое обработчика.
	Если не ЗначениеЗаполнено(ПапкаИдентификатор) Тогда
		Сообщить("Выбери папку для синхронизации");
		Возврат;
	КонецЕсли;

	Попытка 
		Аутлук = Новый COMОбъект("Outlook.Application");
	Исключение
		Сообщить("Не доступен программный интерфейс к Аутлуку");
		Возврат;
	КонецПопытки;

	Для каждого СтрокаСобытия из ТаблицаСобытий Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаСобытия.ИД) Тогда
			Событие = Аутлук.CreateItem(1);
			Событие.Subject = СтрокаСобытия.Тема;;
			Событие.Start = Формат(СтрокаСобытия.ДатаНачала, "ДЛФ=DT"); //в формате 01.01.0001 00:00:00
			Событие.End = Формат(СтрокаСобытия.ДатаОкончания, "ДЛФ=DT"); //в формате 01.01.0001 00:00:00
			Событие.Body = СтрокаСобытия.Тело;
			Событие.ReminderMinutesBeforeStart = 15;
			Событие.BusyStatus = 0;
			Событие.MeetingStatus = 0;
			Событие.Sensitivity = 2;

			Событие.Save();
			СтрокаСобытия.ИД = Событие.EntryID;
		Иначе
			
			Если ЗначениеЗаполнено(СтрокаСобытия.Наряд) Тогда
				Даты = Новый Структура();
				Даты.Вставить("Начало", СтрокаСобытия.ДатаНачала);
				Даты.Вставить("Конец", СтрокаСобытия.ДатаОкончания);
				Даты.Вставить("Наряд", СтрокаСобытия.Наряд);
				ОбновитьНаряд(Даты);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
