
&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьОбщееКоличество(Команда)
	яВсегоФакт = Часы.Итог("Факт");
	Если яВсегоФакт = 0 Тогда
		Сообщить("Нет часов факт для распределения.");
		Возврат;
	КонецЕсли;
	яРаспределить = ОбщееКоличество;
	Для Каждого яС Из Часы Цикл
		яС.Отчет = яС.Факт / яВсегоФакт * ОбщееКоличество;
		яРаспределить = яРаспределить - яС.Отчет;
	КонецЦикла;
	Если яРаспределить <> 0 Тогда
		яС.Отчет = яС.Отчет + яРаспределить;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция Подтвердить_Сервер()
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Часы.Выгрузить());
КонецФункции

&НаКлиенте
Процедура Подтвердить(Команда)
	яРезультат = Подтвердить_Сервер();
	Закрыть(яРезультат);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Данные")
		И ТипЗнч(Параметры.Данные) = Тип("Массив") Тогда
		Для Каждого яС Из Параметры.Данные Цикл
			яНов = Часы.Добавить();
			ЗаполнитьЗначенияСвойств(яНов, яС);
		КонецЦикла;
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОбщееКоличество(Команда)
	ОбщееКоличество = Часы.Итог("Факт");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОбщееКоличество(Неопределено);
КонецПроцедуры
