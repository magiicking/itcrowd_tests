&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Программная") Тогда
		ОбновитьКодКлавиатурыХТМЛ();
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.Авторазмер.ТолькоПросмотр 						= Объект.ВидКлавиатуры <> ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.СкрытьПослеИспользования.ТолькоПросмотр 		= Объект.ВидКлавиатуры <> ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.КнопкиДанныеОтправки.Видимость 				= Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Контекстная");
	Элементы.КнопкиURL.Видимость 							= Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Контекстная");
	Элементы.КнопкиЗапроситьКонтакт.Видимость 				= Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.КнопкиЗапроситьМестоположение.Видимость	 	= Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Стандартная");
	Элементы.ГруппаКнопки.ТолькоПросмотр 					= Объект.ВидКлавиатуры =  ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.СкрытьКлавиатуру");
	
	Элементы.СтраницаОсновная.Видимость 		= НЕ  Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Программная");
	Элементы.СтраницаКодКлавиатуры.Видимость	= Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Программная");
	Элементы.СтраницаРедактирование.Видимость	= Объект.ВидКлавиатуры = ПредопределенноеЗначение("Перечисление.ТелеграмВидыКлавиатуры.Программная");
	
КонецПроцедуры


&НаКлиенте
Процедура ВидКлавиатурыПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры


&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаКодКлавиатуры Тогда
		
		ОбновитьКодКлавиатурыХТМЛ();
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьКодКлавиатурыХТМЛ()
	
	ТелеграмКлиент.ОбновитьКодСборкиОтветаHTML(ЭтаФорма, "КодКлавиатурыХТМЛ", "КодКлавиатуры");
	КодКлавиатурыХТМЛДокументСформирован(Элементы.КодКлавиатурыХТМЛ);
	
КонецПроцедуры


&НаКлиенте
Процедура КодКлавиатурыХТМЛДокументСформирован(Элемент)
	
	Пока СписокКоманд.Количество() > 0 Цикл
		Если ТипЗнч(Элемент.Документ) = Тип("Неопределено") Тогда
			Возврат;
		КонецЕсли;
		Если Элемент.Документ.readyState <> "complete" Тогда
			Возврат;
		КонецЕсли;
		Команда = СписокКоманд.Получить(0).Значение[0];
		Если Команда = "РаскрашиваемТекст" Тогда
			Элемент.Документ.body.innerHTML = СписокКоманд.Получить(0).Значение[1];
		ИначеЕсли Команда = "ИзменитьСтили" Тогда
			// Получим стили HTML документа
			КоллекцияСтилей = Элемент.Документ.styleSheets;
			// Ситуация, когда стилей больше одного, не обрабатывается
			Если КоллекцияСтилей.length >= 1 Тогда 
				// Изменяем стиль на подготовленный
				Стили = КоллекцияСтилей.item(0);
				Стили.cssText = ТелеграмКлиент.ВернутьСтиль();
			Иначе
				Если КоллекцияСтилей.length = 0 Тогда
					НовыйСтиль = Элемент.Документ.createElement("style");
					НовыйСтиль.type = "text/css";
    				НовыйСтиль.innerText = ТелеграмКлиент.ВернутьСтиль();
    				Элемент.Документ.head.appendChild(НовыйСтиль);
					
					//НовыйСтиль = Элемент.Документ.createStyleSheet(, 0);
					//НовыйСтиль.cssText = ТелеграмКлиент.ВернутьСтиль();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		СписокКоманд.Удалить(0);
	КонецЦикла
	
КонецПроцедуры


&НаКлиенте
Процедура КодИзКнопок(Команда)
	
	Код = КодИзКнопокСервер();
	ТД = Новый ТекстовыйДокумент;
	ТД.ДобавитьСтроку("//");
	ТД.ДобавитьСтроку("// Используйте этот код как шаблон для формирования программно задаваемой");
	ТД.ДобавитьСтроку("// клавиатуры (соответствующего типа — контекстная или стандартная)");
	ТД.ДобавитьСтроку("// Также, не забывайте, что при формировании программной клавиатуры доступны НакопленныеДанные");
	ТД.ДобавитьСтроку("//");
	ТД.ДобавитьСтроку(Код);
	ТД.ТолькоПросмотр = Истина;
	ТД.Показать("Код клавиатуры");
	
КонецПроцедуры


&НаСервере
Функция КодИзКнопокСервер()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Кнопки", Объект.Кнопки.Выгрузить());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Кнопки.НомерСтроки КАК НомерСтроки,
	|	Кнопки.Строка КАК Строка,
	|	Кнопки.Текст КАК Текст,
	|	Кнопки.ДанныеОтправки КАК ДанныеОтправки,
	|	Кнопки.URL КАК URL,
	|	Кнопки.ЗапроситьКонтакт КАК ЗапроситьКонтакт,
	|	Кнопки.ЗапроситьМестоположение КАК ЗапроситьМестоположение
	|ПОМЕСТИТЬ Кнопки
	|ИЗ
	|	&Кнопки КАК Кнопки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Кнопки.НомерСтроки КАК НомерСтроки,
	|	Кнопки.Строка КАК Строка,
	|	Кнопки.Текст КАК Текст,
	|	Кнопки.ДанныеОтправки КАК ДанныеОтправки,
	|	Кнопки.URL КАК URL,
	|	Кнопки.ЗапроситьКонтакт КАК ЗапроситьКонтакт,
	|	Кнопки.ЗапроситьМестоположение КАК ЗапроситьМестоположение
	|ИЗ
	|	Кнопки КАК Кнопки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Строка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Строка";
	
	Пакет = Запрос.ВыполнитьПакет();
	РезультатКнопки = Пакет[1];
	
	ВидыКлавиатуры = Перечисления.ТелеграмВидыКлавиатуры;
	
	Код = "";
	
	Если Объект.ВидКлавиатуры = ВидыКлавиатуры.Стандартная Тогда
		
		Код = Код + "МассивСтрок = Новый Массив;" + Символы.ПС;
		
		ВыборкаСтроки = РезультатКнопки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
		Пока ВыборкаСтроки.Следующий() Цикл
			
			Код = Код + Символы.ПС + "МассивКнопокСтроки = Новый Массив;" + Символы.ПС + Символы.ПС;
			
			ВыборкаКнопки = ВыборкаСтроки.Выбрать();
			Пока ВыборкаКнопки.Следующий() Цикл
				
				Код = Код + "Кнопка = Новый Структура;" + Символы.ПС;
				Код = Код + "Кнопка.Вставить(""text"", """ + ВыборкаКнопки.Текст + """);" + Символы.ПС;
				Если ВыборкаКнопки.ЗапроситьКонтакт Тогда
					Код = Код + "Кнопка.Вставить(""request_contact"", Истина);" + Символы.ПС;
				КонецЕсли;
				Если ВыборкаКнопки.ЗапроситьМестоположение Тогда
					Код = Код + "Кнопка.Вставить(""request_location"", Истина);" + Символы.ПС;
				КонецЕсли;
				
				Код = Код + "МассивКнопокСтроки.Добавить(Кнопка);" + Символы.ПС + Символы.ПС;
				
			КонецЦикла;
			
			Код = Код + "МассивСтрок.Добавить(МассивКнопокСтроки);" + Символы.ПС;
			
		КонецЦикла;
		
		Код = Код + Символы.ПС;
		Код = Код + "Клавиатура = Новый Структура;" + Символы.ПС;
		Код = Код + "Клавиатура.Вставить(""keyboard"", МассивСтрок);" + Символы.ПС;
		
		Если Объект.Авторазмер Тогда
			Код = Код + "Клавиатура.Вставить(""resize_keyboard"", Истина);" + Символы.ПС;
		КонецЕсли;
		
		Если Объект.СкрытьПослеИспользования Тогда
			Код = Код + "Клавиатура.Вставить(""one_time_keyboard"", Истина);" + Символы.ПС;
		КонецЕсли;
		
	ИначеЕсли Объект.ВидКлавиатуры = ВидыКлавиатуры.Контекстная Тогда
		
		
		Код = Код + "МассивСтрок = Новый Массив;" + Символы.ПС + Символы.ПС;
		
		ВыборкаСтроки = РезультатКнопки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
		Пока ВыборкаСтроки.Следующий() Цикл
			
			Код = Код + "МассивКнопокСтроки = Новый Массив;" + Символы.ПС + Символы.ПС;
			
			ВыборкаКнопки = ВыборкаСтроки.Выбрать();
			Пока ВыборкаКнопки.Следующий() Цикл
				
				Код = Код + "Кнопка = Новый Структура;" + Символы.ПС;
				Код = Код + "Кнопка.Вставить(""text"", """ + ВыборкаКнопки.Текст + """);" + Символы.ПС;
				Если ЗначениеЗаполнено(ВыборкаКнопки.URL) Тогда
					Код = Код + "Кнопка.Вставить(""url"", """ + ВыборкаКнопки.URL + """);" + Символы.ПС;
				ИначеЕсли ЗначениеЗаполнено(ВыборкаКнопки.ДанныеОтправки) Тогда
					Код = Код + "Кнопка.Вставить(""callback_data"", """ + ВыборкаКнопки.ДанныеОтправки + """);" + Символы.ПС;
				КонецЕсли;
				
				Код = Код + "МассивКнопокСтроки.Добавить(Кнопка);" + Символы.ПС + Символы.ПС;
				
			КонецЦикла;
			
			Код = Код + "МассивСтрок.Добавить(МассивКнопокСтроки);" + Символы.ПС;
			
		КонецЦикла;
		
		Код = Код + Символы.ПС;
		Код = Код + "Клавиатура = Новый Структура;" + Символы.ПС;
		Код = Код + "Клавиатура.Вставить(""inline_keyboard"", МассивСтрок);" + Символы.ПС;
		
	КонецЕсли;
	
	Возврат Код;
	
КонецФункции


&НаКлиенте
Процедура НакопленныеДанные(Команда)
	
	Объект.КодКлавиатуры = НакопленныеДанныеПодсказка() + Объект.КодКлавиатуры;
	
КонецПроцедуры


&НаСервере
Функция НакопленныеДанныеПодсказка()
	
	Возврат ТелеграмСервер.ПодсказкаПоСтруктуреНакопленныеДанные();
	
КонецФункции


&НаКлиенте
Процедура ФорматОтвета(Команда)
	
	Объект.КодКлавиатуры = Объект.КодКлавиатуры + Символы.ПС + 
	"// Ожидается переменная Клавиатура типа <Структура>.
	|// Формат структуры можно увидеть, нажав кнопку ""Код клавиатуры"" страницы Основная стандартной или контекстной клавиатуры";
	
КонецПроцедуры