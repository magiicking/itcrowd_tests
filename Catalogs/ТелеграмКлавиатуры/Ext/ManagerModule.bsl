Функция СобратьКлавиатуру(КлавиатураИлиИдентификатор, НакопленныеДанные) Экспорт
	
	Перем Ответ;
	
	Если ТипЗнч(КлавиатураИлиИдентификатор) = Тип("СправочникСсылка.ТелеграмКлавиатуры") Тогда
		Клавиатура = КлавиатураИлиИдентификатор;
	Иначе
		Клавиатура = Справочники.ТелеграмКлавиатуры.НайтиПоНаименованию(КлавиатураИлиИдентификатор);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Клавиатура", Клавиатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмКлавиатуры.ВидКлавиатуры КАК ВидКлавиатуры,
	|	ТелеграмКлавиатуры.Авторазмер КАК Авторазмер,
	|	ТелеграмКлавиатуры.СкрытьПослеИспользования КАК СкрытьПослеИспользования,
	|	ТелеграмКлавиатуры.КодКлавиатуры КАК КодКлавиатуры
	|ИЗ
	|	Справочник.ТелеграмКлавиатуры КАК ТелеграмКлавиатуры
	|ГДЕ
	|	ТелеграмКлавиатуры.Ссылка = &Клавиатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТелеграмКлавиатурыКнопки.Строка КАК Строка,
	|	ТелеграмКлавиатурыКнопки.НомерСтроки КАК НомерСтроки,
	|	ТелеграмКлавиатурыКнопки.Текст КАК Текст,
	|	ТелеграмКлавиатурыКнопки.URL КАК URL,
	|	ТелеграмКлавиатурыКнопки.ДанныеОтправки КАК ДанныеОтправки,
	|	ТелеграмКлавиатурыКнопки.ЗапроситьКонтакт КАК ЗапроситьКонтакт,
	|	ТелеграмКлавиатурыКнопки.ЗапроситьМестоположение КАК ЗапроситьМестоположение
	|ИЗ
	|	Справочник.ТелеграмКлавиатуры.Кнопки КАК ТелеграмКлавиатурыКнопки
	|ГДЕ
	|	ТелеграмКлавиатурыКнопки.Ссылка = &Клавиатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	Строка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Строка";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	РезультатКлавиатура = Пакет[0];
	ВыборкаКлавиатура 	= РезультатКлавиатура.Выбрать();
	ВыборкаКлавиатура.Следующий();
	ВидыКлавиатуры 	= Перечисления.ТелеграмВидыКлавиатуры;
	ВидКлавиатуры 	= ВыборкаКлавиатура.ВидКлавиатуры;
	РезультатКнопки	= Пакет[1];
	
	Если ВидКлавиатуры = ВидыКлавиатуры.Программная Тогда
		Ответ = СобратьКлавиатуруПрограммная(ВыборкаКлавиатура.КодКлавиатуры, НакопленныеДанные);
	Иначе
		Если ВидКлавиатуры = ВидыКлавиатуры.Стандартная Тогда
			МассивСтрок = Новый Массив;
			ВыборкаСтроки = РезультатКнопки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
			Пока ВыборкаСтроки.Следующий() Цикл
				ВыборкаКнопки = ВыборкаСтроки.Выбрать();
				МассивРядаКнопок = Новый Массив;
				Пока ВыборкаКнопки.Следующий() Цикл
					СтруктураКнопки = Новый Структура;
					СтруктураКнопки.Вставить("text", ВыборкаКнопки.Текст);
					Если ВыборкаКнопки.ЗапроситьКонтакт Тогда
						СтруктураКнопки.Вставить("request_contact", ВыборкаКнопки.ЗапроситьКонтакт);
					КонецЕсли;
					Если ВыборкаКнопки.ЗапроситьМестоположение Тогда
						СтруктураКнопки.Вставить("request_location", ВыборкаКнопки.ЗапроситьМестоположение);
					КонецЕсли;
					МассивРядаКнопок.Добавить(СтруктураКнопки);
				КонецЦикла;
				МассивСтрок.Добавить(МассивРядаКнопок);
			КонецЦикла;
			
			Ответ = Новый Структура;
			Ответ.Вставить("keyboard", МассивСтрок);
			Если ВыборкаКлавиатура.Авторазмер Тогда
				Ответ.Вставить("resize_keyboard", ВыборкаКлавиатура.Авторазмер);
			КонецЕсли;
			Если ВыборкаКлавиатура.СкрытьПослеИспользования Тогда
				Ответ.Вставить("one_time_keyboard", ВыборкаКлавиатура.СкрытьПослеИспользования);
			КонецЕсли;
			
		ИначеЕсли ВидКлавиатуры = ВидыКлавиатуры.Контекстная Тогда
			МассивСтрок = Новый Массив;
			ВыборкаСтроки = РезультатКнопки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Строка");
			Пока ВыборкаСтроки.Следующий() Цикл
				ВыборкаКнопки = ВыборкаСтроки.Выбрать();
				МассивРядаКнопок = Новый Массив;
				Пока ВыборкаКнопки.Следующий() Цикл
					СтруктураКнопки = Новый Структура;
					СтруктураКнопки.Вставить("text", ВыборкаКнопки.Текст);
					Если ЗначениеЗаполнено(ВыборкаКнопки.URL) Тогда
						СтруктураКнопки.Вставить("url", ВыборкаКнопки.URL);
					ИначеЕсли ЗначениеЗаполнено(ВыборкаКнопки.ДанныеОтправки) Тогда
						СтруктураКнопки.Вставить("callback_data", ВыборкаКнопки.ДанныеОтправки);
					КонецЕсли;
					МассивРядаКнопок.Добавить(СтруктураКнопки);
				КонецЦикла;
				МассивСтрок.Добавить(МассивРядаКнопок);
			КонецЦикла;
			Ответ = Новый Структура;
			Ответ.Вставить("inline_keyboard", МассивСтрок);
			
		ИначеЕсли ВидКлавиатуры = ВидыКлавиатуры.СкрытьКлавиатуру Тогда
			Ответ = Новый Структура;
			Ответ.Вставить("hide_keyboard", Истина);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции


Функция СобратьКлавиатуруПрограммная(КодСборки, НакопленныеДанные)
	
	Перем Клавиатура;
	Выполнить(КодСборки);
	Возврат Клавиатура;
	
КонецФункции