&НаКлиенте
Процедура ДобавитьФайл_Завершение(Результат, Адрес, 
	ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ДобавитьФайл_Завершение_Сервер(Адрес, ВыбранноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайл_Завершение_Сервер(Адрес, ВыбранноеИмяФайла)
	
	яДвДанные = ПолучитьИзВременногоХранилища(Адрес);
	яФ = Новый Файл(ВыбранноеИмяФайла);
	яИмяФайла = ПолучитьИмяВременногоФайла(яФ.Расширение);
	Попытка
		яДвДанные.Записать(яИмяФайла);
		яОбработка = ВнешниеОбработки.Создать(яИмяФайла);     
		яИД = яОбработка.Метаданные().Имя;
		Если яИД <> Объект.Идентификатор И ЗначениеЗаполнено(Объект.Идентификатор) Тогда
			Сообщить("Идентификатор обработки не совпадает с именем из файла. Операция прервана.");
			Возврат;
		КонецЕсли;
		яСведения = яОбработка.СведенияОВнешнейОбработке();
		УдалитьФайлы(яИмяФайла);
		
		яВерсия = яСведения.Версия;
		Объект.Описание = яСведения.Информация;
		Объект.Наименование = яСведения.Наименование;
		Объект.Идентификатор = яИД;
		
		Модифицированность = Истина;
		Если Объект.Ссылка.Пустая() Тогда
			Сообщить("Необходимо записать объект и повторить добавление обработки из пакета.");
			Возврат;
		КонецЕсли;
		
		яЗ = Новый Запрос(
		"ВЫБРАТЬ
		|	ICMP_ВерсииПакета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ICMP_ВерсииПакета КАК ICMP_ВерсииПакета
		|ГДЕ
		|	ICMP_ВерсииПакета.Владелец = &Владелец
		|	И ICMP_ВерсииПакета.Версия = &Версия");
		яЗ.УстановитьПараметр("Владелец", Объект.Ссылка);
		яЗ.УстановитьПараметр("Версия", яВерсия);
		яВ = яЗ.Выполнить().Выбрать();
		Если яВ.Следующий() Тогда
			яОб = яВ.Ссылка.ПолучитьОбъект();
		Иначе
			яОб = Справочники.ICMP_ВерсииПакета.СоздатьЭлемент();
		КонецЕсли;
		яОб.Версия = яВерсия;
		яОб.Владелец = Объект.Ссылка;
		яОб.Содержимое = Новый ХранилищеЗначения(яДвДанные, Новый СжатиеДанных(1));
		яОб.ИмяФайлаОбработки = яФ.Имя;
		Если ПустаяСтрока(яОб.Наименование) Тогда
			яОб.Наименование = "Версия " + яВерсия;
		КонецЕсли;
		яОб.Записать();
		
	Исключение
		яОшибка = ОписаниеОшибки();
		Сообщить(яОшибка);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	яОповещение = Новый ОписаниеОповещения("ДобавитьФайл_Завершение", ЭтаФорма);
	НачатьПомещениеФайла(яОповещение,,,Истина);
КонецПроцедуры
